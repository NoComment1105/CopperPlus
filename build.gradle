//file:noinspection GradlePackageVersionRange
plugins {
	id 'org.quiltmc.loom' version '0.12+'
	id 'org.quiltmc.gradle.licenser' version '1.+'
	id 'maven-publish'
}

def targetJavaVersion = 17

group = project.maven_group
version = "$project.version+${project.minecraft}"
archivesBaseName = project.archives_base_name

repositories {
	maven {
		name = 'Modrinth'
		url = 'https://api.modrinth.com/maven'
		content {
			includeGroup 'maven.modrinth'
		}
	}
	maven {
		name = 'Terraformers'
		url = uri('https://maven.terraformersmc.com/releases/')
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft}"
	mappings(loom.layered {
		officialMojangMappings()
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${project.minecraft}+build.${project.quilt_mappings}:v2")
	})
	modImplementation("org.quiltmc:quilt-loader:${project.quilt_loader}")
	modImplementation("org.quiltmc.quilted-fabric-api:quilted-fabric-api:${project.quilted_fabric_api}")

	// Mods to aid development speed
	modRuntimeOnly("maven.modrinth:gvQqBUqZ:${project.lithium}") // Lithium
	modRuntimeOnly("maven.modrinth:hvFnDODi:${project.lazydfu}") // LazyDFU

	modImplementation("com.terraformersmc:modmenu:${project.modmenu}") {
		transitive(false)
	}
}

java {
	sourceCompatibility = JavaVersion.toVersion(targetJavaVersion)
	targetCompatibility = JavaVersion.toVersion(targetJavaVersion)

	withSourcesJar()

	// If this mod is going to be a library, its better to generate javadocs too, uncomment this line to generate them
	// withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding('UTF-8')
	it.options.deprecation(true)
	it.options.incremental(true)
	it.options.release.set(targetJavaVersion)
}

jar {
	from('LICENSE') {
		rename { "${it}_${archivesBaseName}"}
	}
}

license {
	rule file('HEADER')
	include '**/*.java'
}

processResources {
	inputs.property 'version', version

	filesMatching('quilt.mod.json') {
		expand(
				'version': version,
		)
	}
}


// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {
		 //mavenLocal() // Publish to the local maven
	}
}

runClient {
	// Export mixin altered classes to the build output folder and set memory limit
	jvmArgs('-Dmixin.debug.export=true', '-Xmx1536m')
}

quiltflower {
	addToRuntimeClasspath.set(true)
}
